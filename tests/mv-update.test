# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2019 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## mirror-vcs . . update STORE URL FIRST

kt check Tcl     8.5
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt::source support/invoke-vcs.tcl

# # ## ### ##### ######## ############# #####################

foreach {vcs urlcmd} {
    fossil  a-core
    git     a-github
    github  c-github
    hg      d-hg
    svn     e-svn
} {
    test mirror-vcs-${vcs}-update-1.0 "mirror-vcs $vcs update - wrong args, not enough" -body {
	mvcs $vcs LOG update
    } -result [err mirror-vcs/update/wrongargs]
    
    test mirror-vcs-${vcs}-update-1.1 "mirror-vcs $vcs update - wrong args, not enough" -body {
	mvcs $vcs LOG update STORE
    } -result [err mirror-vcs/update/wrongargs]
    
    test mirror-vcs-${vcs}-update-1.2 "mirror-vcs $vcs update - wrong args, not enough" -body {
	mvcs $vcs LOG update STORE URL
    } -result [err mirror-vcs/update/wrongargs]
    
    test mirror-vcs-${vcs}-update-1.3 "mirror-vcs $vcs update - wrong args, too many" -body {
	mvcs $vcs LOG update STORE URL FIRST X
    } -result [err mirror-vcs/update/wrongargs]
    
    test mirror-vcs-${vcs}-update-2.0 "mirror-vcs $vcs update - bad store" -body {
	mvcs $vcs LOG update BADSTORE URL FIRST
    } -result [err mirror-vcs/bad-store]

    if {$vcs ne "svn"} {
	# svn skips badurl check of update. svn ignores the url
	# argument and simply updates the repository, which knows it
	# sole remote to talk to.
	
	test mirror-vcs-${vcs}-update-2.1 "mirror-vcs $vcs update - bad url" -setup {
	    file mkdir STORE
	    mvcs $vcs LOG setup STORE [$urlcmd]
	} -body {
	    mvcs $vcs LOG update STORE BADURL 0
	} -cleanup {
	    file delete -force STORE
	} -match glob -result [err mirror-vcs/update/b-${vcs}]
    }
    
    test mirror-vcs-${vcs}-update-2.2 "mirror-vcs $vcs update - bad flag" -setup {
	file mkdir STORE
    } -body {
	mvcs $vcs LOG update STORE [$urlcmd] FIRST
    } -cleanup {
	file delete -force STORE
    } -match glob -result [err mirror-vcs/update/badflag]
    
    # Skip plugins without example repository
    if {$urlcmd eq {}} continue

    foreach first {0 1} {
	test mirror-vcs-${vcs}-update-3.0.$first "mirror-vcs $vcs update result, first = $first" -setup {
	    set r {}
	    file mkdir STORE
	    mvcs $vcs LOG setup STORE [$urlcmd]
	} -body {
	    lappend r [mvcs $vcs LOG update STORE [$urlcmd] $first]
	    lappend r [string trim [fileutil::cat LOG]]
	} -cleanup {
	    unset r
	    file delete -force LOG STORE
	} -match glob -result [list \
	   [ok mirror-vcs/update/p-$vcs$first] \
	   [V  mirror-vcs/update/$vcs$first]]
    }
}
    
# # ## ### ##### ######## ############# #####################
cleanupTests
return
