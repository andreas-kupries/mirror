# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2018 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## mail configuration commands

kt check Tcl     8.5
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt::source support/invoke.tcl

# # ## ### ##### ######## ############# #####################

lappend cases  footer . {}
lappend cases  header . {}
lappend cases  host   . {}
lappend cases  pass   . {}
lappend cases  port   4 {0 -1}
lappend cases  sender . {}
lappend cases  tls    1 {2 bogus}
lappend cases  user   . {}

foreach {
    command
    goodvalue
    badvalues
} $cases {
    test mail-${command}-1.0 "mail ${command} - wrong args, too many" -body {
	mapp mail ${command} $goodvalue foo
    } -result [err mail/${command}/wrongargs-toomany] -match glob

    test mail-${command}-2.0 "mail ${command} query, default" -body {
	mapp mail ${command}
    } -result [ok mail/${command}/default]

    test mail-${command}-2.1 "${command} change, ok, $goodvalue" -body {
	mapp mail ${command} $goodvalue
    } -result [ok mail/${command}/ok]

    foreach bad $badvalues {
	incr k
	test mail-${command}-2.2.$k "${command} change, bad, $bad" -body {
	    mapp mail ${command} $bad
	} -result [err mail/${command}/bad-$bad] -match glob
    }
}

unset cases

# # ## ### ##### ######## ############# #####################
cleanupTests
return
